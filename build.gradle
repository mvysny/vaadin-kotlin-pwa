buildscript {
    ext.karibudsl_version = '0.3.3'
    ext.vaadinonkotlin_version = '0.4.0'
    ext.vaadin10_version = '10.0.0.alpha21'
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.2.21"
    id "org.akhikhl.gretty" version "2.0.0"
    id "io.spring.dependency-management" version "1.0.4.RELEASE"  // remove when https://github.com/gradle/gradle/issues/4417 is fixed
}

apply plugin: 'war'

defaultTasks 'clean', 'build'

repositories {
    mavenCentral()
    maven { url "https://repo.vaadin.com/nexus/content/repositories/flow" }
    maven { url 'https://maven.vaadin.com/vaadin-addons' }
    maven { url 'https://maven.vaadin.com/vaadin-prereleases' }
    maven { url "https://dl.bintray.com/mvysny/vaadin-on-kotlin" }
    maven { url "https://dl.bintray.com/mvysny/github" }
    maven { url 'https://dl.bintray.com/webjars/maven' }
}

gretty {
    contextPath = ''
}

dependencyManagement {
    imports { mavenBom "com.vaadin:vaadin-bom:$vaadin10_version" }
}

test {
    useJUnitPlatform()
}

dependencies {
    // Karibu-DSL dependency, includes Vaadin
    compile("com.github.vok.karibudsl:karibu-dsl-v10:$karibudsl_version")
    compile "com.github.vok:vok-framework-v10-sql2o:$vaadinonkotlin_version"
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // logging
    // currently we are logging through the SLF4J API to LogBack. See src/main/resources/logback.xml file for the logger configuration
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'org.slf4j:slf4j-api:1.7.25'

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.21"

    // db
    compile 'org.flywaydb:flyway-core:5.0.7'
    compile 'com.h2database:h2:1.4.196'

    // test support
    testCompile "com.github.vok.karibudsl:karibu-testing-v10:$karibudsl_version"
    testCompile "com.github.mvysny.dynatest:dynatest:0.6"

    // heroku app runner
    testRuntime 'com.github.jsimone:webapp-runner:8.5.11.3'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

// Heroku
task stage(dependsOn: ['build'])
task copyToLib(type: Copy) {
    into "$buildDir/server"
    from(configurations.testRuntime) {
        include "webapp-runner*"
    }
}

stage.dependsOn(copyToLib)
